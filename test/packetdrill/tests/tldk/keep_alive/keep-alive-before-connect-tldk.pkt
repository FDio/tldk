// Test set keepalive before connect

// enable keepalive and set args
0   socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
+0  setsockopt(3, SOL_SOCKET, SO_KEEPALIVE, [1], 4) = 0
+0  setsockopt(3, IPPROTO_TCP, TCP_KEEPIDLE, [5], 4) = 0
+0  setsockopt(3, IPPROTO_TCP, TCP_KEEPINTVL, [2], 4) = 0
+0  setsockopt(3, IPPROTO_TCP, TCP_KEEPCNT, [2], 4) = 0

// Establish a connection.
+0 fcntl(3, F_GETFL) = 0x2 (flags O_RDWR)
+0 fcntl(3, F_SETFL, O_RDWR|O_NONBLOCK) = 0
+0 connect(3, ..., ...) = -1 EINPROGRESS (Operation now in progress)
+0 > S 0:0(0) <...>
+0 < S. 0:0(0) ack 1 win 32792 <mss 1460>
+0 > . 1:1(0) ack 1 <...>

// first keep-alive pkt, idle = 5
+4 ~ +6 > . 0:0(0) ack 1

// second keep-alive pkt, interval = 2
+1 ~ +3 > . 0:0(0) ack 1

// get response, idle = 5
+0 < . 1:1(0) ack 1 win 32792

// first keep-alive pkt again, idle = 5
+4 ~ +6 > . 0:0(0) ack 1

// second keep-alive pkt, interval = 2
+1 ~ +3 > . 0:0(0) ack 1

// get no response, send rst
+1 ~ +3 > R. 1:1(0) ack 1

+1 read(3, ..., 10) = -1 ETIMEDOUT (Connection timed out)
+0 close(3) = 0